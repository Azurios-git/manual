theory ChannelExample_auth
begin


/* Alice&Bob specification (sent over an authentic channel)

	I: 	fresh(n)
	I -> R: n 

*/

/* Channel rules */

rule ChanOut_A:
	[ Out_A($A,$B,x) ]
	--[ ChanOut_A($A,$B,x) ]->
	[ !Auth($A,x), Out(<$A,$B,x>) ]

rule ChanIn_A:
	[ !Auth($A,x), In($B) ]
	--[ ChanIn_A($A,$B,x) ]->
	[ In_A($A,$B,x) ]

/* Protocol */

rule I_1: 
	[ Fr(~n) ]
 	--[ Send($I,$R,~n), SecretI($I,~n) ]-> 
	[ Out_A($I,$R,~n) ]
		
rule R_1:
	[ In_A($I,$R,~n)  ]
	--[ SecretR($R,~n), Authentic($R,$I,~n) ]->
	[ ]

/* Security Properties */

lemma nonce_secret_initiator: 
	"All R n #i #j. SecretI(R,n) @i & K(n) @j ==> F"

lemma nonce_secret_receiver: 
	"All R n #i #j. SecretR(R,n) @i & K(n) @j ==> F"

lemma message_authentication: 
	"All R I n #j. Authentic(R,I,n) @j ==> Ex R2 #i. Send(I,R2,n) @i &i<j"

end

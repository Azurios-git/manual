theory ChannelExample
begin


/* built-ins */
builtins: signing, asymmetric-encryption, hashing, symmetric-encryption
functions: otpw/1, pw/1 


/* Channel rules */

rule ChanOut_A:
	[Out_A($A,$B,x)]
	--[ChanOut_A($A,$B,x)]->
	[!Auth($A,x), Out(<$A,$B,x>)]

rule ChanIn_A:
	[!Auth($A,x), In($B)]
	--[ChanIn_A($A,$B,x)]->
	[In_A($A,$B,x)]

rule ChanOut_C:
	[Out_C($A,$B,x)]
	--[ChanOut_C($A,$B,x)]->
	[!Conf($B,x)]

rule ChanIn_C:
	[!Conf($B,x), In($A)]
	--[ChanIn_C($A,$B,x)]->
	[In_C($A,$B,x)]

rule ChanIn_CAdv:
	[In(<$A,$B,x>)]
	--[]->
	[In_C($A,$B,x)]

rule ChanOut_S:
	[Out_S($A,$B,x)]
	--[ChanOut_S($A,$B,x)]->
	[!Sec($A,$B,x)]

rule ChanIn_S:
	[!Sec($A,$B,x)]
	--[ChanIn_S($A,$B,x)]->
	[In_S($A,$B,x)]


rule I_1: 
	[ Fr(~n) ]
 	--[ Send($I,$R,~n), SecretI($I,~n) ]-> 
	[ Out_C($I,$R,~n) ]
		
rule R_1:
	[ In_C($I,$R,~n)  ]
	--[ SecretR($R,~n), Authentic($R,$I,~n) ]->
	[ ]


lemma nonce_secret_initiator: 
	"All R n #i #j. SecretI(R,n) @i & K(n) @j ==> F"

lemma nonce_secret_receiver: 
	"All R n #i #j. SecretR(R,n) @i & K(n) @j ==> F"

lemma message_authentication: 
	"All R I n #j. Authentic(R,I,n) @j ==> Ex R2 #i. Send(I,R2,n) @i &i<j"






end

 
